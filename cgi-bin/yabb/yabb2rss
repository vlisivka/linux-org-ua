#!/usr/bin/perl

# TODO
# * use settings for hostmaster and board name information
# * allow rss on individual threads, recent, new messages
#   probably it's easier to do in yabb by adding alternative
#   representation method
# * handle unclosed html tags (<a>[cut]</a>)

use strict;
use utf8;

use DateTime;
use CGI qw/param url/;

require "./bbcode.pl";

our ($boardurl, $boardsdir, $datadir);
require "./Paths.pl";
my $yabburl   = "$boardurl/YaBB.pl";
my $scripturl = url ();

our ( $mloaded, @categoryorder, %cat, %catinfo, %board);
require "$boardsdir/forum.master";

# Return list of all available groups
sub all_groups() {
  my @groups=();
  foreach my $catName (@categoryorder)
  {
    push @groups, split(/,/, $cat{$catName});
  }
  return @groups;
}

my $tz = DateTime::TimeZone -> new ( name => 'local' );
# Formats time for RSS (RFC822)
sub rss_format_time {
	my ( $time ) = @_;
	return DateTime -> from_epoch ( time_zone => $tz, epoch => $time ) -> strftime ( "%a, %d %b %Y %H:%M:%S %z" );
}

if(param('help'))
{
  my $all_groups=join(', ', all_groups());
  print <<HELP;
Content-type: text/plain; charset=UTF-8

Usage:
  yabb2rss?group=NAME[&group=NAME2&...][&body=yes[&cdata=yes]]

 group=NAME    group (board) ID
 body=0    don\'t include message body into RSS (default)
 body=1    include message body into RSS
 cdata=0   don\'t include body into <![CDATA[ ]]> brackets
 cdata=1   include body into <![CDATA[ ]]> brackets
           (now default, no workarounds needed)
 encoding  convert text to another encoding , eg. encoding=UTF-8
 maxlength=LENGTH maximum allowed length of message body
           default is 350. Set to 0 to disable limit.
 
 Available groups: $all_groups.
HELP
  
  exit(0);
}

my @groups           = param('group');
my $withDescriptions = param('body');
my $cdata            = param('cdata') || 1;
my $encoding         = param('encoding');
my $len_limit        = param('maxlength');

if(not defined $len_limit or $len_limit !~ /^[0-9]+$/) {
    $len_limit = 350;
}

if($encoding ne '' && $encoding=~m/^([a-zA-Z0-9_-]+)$/) {
	$encoding=$1;
} else {
	$encoding='UTF-8';
}

binmode STDOUT, ":encoding($encoding)";
binmode STDERR, ":utf8";

#Для відлагодки
if(scalar(@groups)==0)
{
  @groups=all_groups();
}

@groups=map { /([a-zA-Z0-9_-]+)/; $1} @groups;

print "Content-Type: text/xml; charset=$encoding\r\n\r\n";

my $doctype='<!DOCTYPE rss PUBLIC "-//Netscape Communications//DTD RSS 0.91//EN" "http://my.netscape.com/publish/formats/rss-0.91.dtd" >';
$doctype='' if(scalar(@groups)>1 or ($withDescriptions and not $cdata));

print <<HEADER;
<?xml version="1.0" encoding="$encoding" ?>
<?xml-stylesheet type="text/xsl" href="/include/rss-view.xsl"?>
$doctype
<rss version="0.91">
HEADER

for my $group(@groups)
{

# Use group name for title
  my ($title, undef)= split /\|/,$board{$group};
  my $description="";

  print <<CHANEL_HEADER;
<channel>
 <title>$title</title>
 <link>$yabburl?board=$group</link>
 <description>$description</description>
 <language>uk-ua</language>
 <copyright>linux.org.ua community</copyright>
 <managingEditor>vlisivka\@gmail.com</managingEditor>
 <webMaster>vlisivka\@gmail.com</webMaster>
 <docs>$scripturl?help=1</docs>
 
 <image>
  <title>linux.org.ua</title>
  <url>http://linux.org.ua/images/linux-org-ua.gif</url>
  <link>$yabburl?board=$group</link>
  <width>88</width>
  <height>31</height>
 </image>
 
CHANEL_HEADER

#Read posts headers
#FIXME: Мабуть не варто читати більше сотні заголовків за раз
open(POSTS,"<$boardsdir/$group.txt");
binmode POSTS, ':utf8';
my @posts=<POSTS>;
close(POSTS);
@posts=sort {(split('\|',$b,2))[0] <=> (split('\|',$a,2))[0]} @posts;

#Display posts
my $count=0;
for my $line(@posts)
{
  chomp $line;
  my ($ctime,$subject,$userName,$userEmail,$date,$replies,$login,$icon,$status)=split('\|',$line);

	# Filter moved threads
  next if($status =~ /m/);
	# Number of items in rss
  last if($count++>=10);

  $subject =~ s/&/\&amp;/ig; 
  $subject =~ s/\[ch(\d{3,})\]/&#$1;/sig;
  $subject =~ s/&nbsp;/\&#160;/ig;

	$date = rss_format_time ( $date ); 

  if($withDescriptions)
  {#Просять включати тіло новини
  
    #Read post body
    open(MESSAGES,"<$datadir/$ctime.txt");
    binmode MESSAGES, ':utf8';
    my $firstMessage=<MESSAGES>;
    close(MESSAGES);
    
    my ($subject2,$userName2,$userEmail2,$date2,$login2,$icon2,$attach,$ip,$body,$ns,$mlm,$mlmb,$msf,$mfn)=split('\|',$firstMessage,11);


    # Cut too long bodies (may break markup)
    if ($len_limit and $len_limit < length ($body)) {
			# Crude cutting on the end of word...
			$body =  substr ($body, 0, $len_limit);
			$body =~ s/\s+\S*$//o;
			$body =~ s/<[^>]*$//; # eliminate possible broken tags FIXME does not solve <a> [cut] </a> problem
			$body .= " ...";
    }

    $body = parseBBCode($body);

    # Шукаєм та заміняєм nbsp на #160 в темі та тілі повідомлення
#    $body =~ s/&/\&amp;/ig; 
    $body =~ s/&nbsp;/\&#160;/ig;
#    $body =~ s/\[ch(\d{3,})\]/&#$1;/sig;

    # Обрізаєм повідомлення після горизонтальної лінійки
    $body=~s{(<br />)?<hr />(.*)$}{"<br /><a href=\"$yabburl?num=$ctime\" class=\"tail\">...(".length($2)." ".plural(length($2),"символ","символи","символів").").</a>"}se;

    
    #Просять взяти тіло повідомлення в лапки, як по стандарту
    $body="<![CDATA[$body]]>" if($cdata);

    print <<ITEM;
<item>
 <title>$subject</title>
 <link>$yabburl?num=$ctime</link>
 <guid>$yabburl?num=$ctime/0</guid>
 <comments>$yabburl?action=post;num=$ctime;title=PostReply</comments>
 <pubDate>$date</pubDate>
 <description>$body</description>
</item>
ITEM
  }else
  {
    print <<ITEM;
<item>
 <title>$subject</title>
 <link>$yabburl?num=$ctime</link>
 <guid>$yabburl?num=$ctime</guid>
 <comments>$yabburl?action=post;num=$ctime;title=PostReply</comments>
 <pubDate>$date</pubDate>
</item>
ITEM
  }

}

  print <<CHANEL_FOOTER;
</channel>
CHANEL_FOOTER
  }

print <<FOOTER;
</rss>
FOOTER

# vim: se ts=2 sw=2: #
